#!ipxe

iseq ${net0/mac:hexhyp} ac-1f-6b-22-61-58 && set net 0 ||
iseq ${net1/mac:hexhyp} ac-1f-6b-22-61-58 && set net 1 ||
isset ${net} || set net 0

set net${net}/ip 104.218.233.98
set net${net}/netmask 255.255.255.248
set net${net}/gateway 104.218.233.97
set linuxif eth${net}
set dns 1.1.1.1
ifopen net${net}

ifstat ||
route ||

set base-uri https://dl-cdn.alpinelinux.org/alpine
set arch x86_64
set version v3.16
set repos-uri ${base-uri}/${version}
set releases-uri ${base-uri}/${version}/releases/${arch}

set kernel-uri ${releases-uri}/netboot/vmlinuz-lts
set ucode-uri http://mirror.rackspace.com/archlinux/iso/latest/arch/boot/intel-ucode.img
set initrd-uri ${releases-uri}/netboot/initramfs-lts
set modloop-uri ${releases-uri}/netboot/modloop-lts
set repo-uri ${repos-uri}/main

set console-param console=ttyS2,115200n8
set ip-param ip=${ip}::${gateway}:${netmask}::${linuxif}:none:${dns}
set ssh-key-param ssh_key="sk-ssh-ed25519@openssh.com AAAAGnNrLXNzaC1lZDI1NTE5QG9wZW5zc2guY29tAAAAIMg4VwM98UqYkExOFbAB2w7R9sR/RlA/lxV1Xwy7S02oAAAABHNzaDo="
set repo-param alpine_repo=${repo-uri}
set modules-param modules=loop,squashfs modloop=${modloop-uri}

set cmdline initrd=intel-ucode.img initrd=initrd.magic ${console-param} ${ip-param} ${repo-param} ${modules-param} ${ssh-key-param}

kernel ${kernel-uri} ${cmdline}
initrd ${ucode-uri}
initrd ${initrd-uri}
boot -ar
